plugins {
    id 'java'
    id 'maven-publish'
}

group 'me.senseiwells'
version '1.0.0'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

sourceSets {
    main {
        java.srcDirs 'src/main/java'
        resources.setSrcDirs(['src/main/resources'])
    }
    test {
        java.srcDirs 'src/test/java'
        resources.setSrcDirs(['src/test/resources'])
    }
}

configurations {
    // implementations that should be included in the jar file
    packImplementation {
        transitive false
    }

    implementation.extendsFrom(packImplementation)
}

dependencies {
    packImplementation 'net.dv8tion:JDA:5.0.0-alpha.9'
    packImplementation 'ch.qos.logback:logback-classic:1.2.11'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation 'com.github.senseiwells:Arucas:9da55dd9be'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

jar {
    from "LICENSE"
    from {
        configurations.packImplementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
    // No manifest since there's no main class
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.senseiwells'
            artifactId = 'ArucasDiscordAPI'
            version = '1.0.0'

            from components.java
        }
    }
}